plugins {
    id 'java-library'
    id 'idea'
    id 'com.vanniktech.maven.publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    tor
}

dependencies {
    // Compile against obsolete version 3.12.x but don't ship it, so that
    // library users can supply either 4.x or 3.12.x depending on whether they
    // need Android 4 compatibility
    compileOnly 'com.squareup.okhttp3:okhttp:3.12.13'

    api 'com.fasterxml.jackson.core:jackson-databind:2.13.4'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.briarproject:null-safety:0.1'
    implementation 'org.briarproject:socks-socket:0.1'

    // Linux lyrebird binary is only used for testing
    tor 'org.briarproject:lyrebird-linux:0.5.0-3'

    // Test with obsolete version 3.12.x to ensure library users can use it
    // if they need Android 4 compatibility
    testImplementation 'com.squareup.okhttp3:okhttp:3.12.13'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

test {
    useJUnitPlatform()
}

if (RELEASE_SIGNING_ENABLED != 'false') {
    signing {
        useGpgCmd()
    }
}

def torBinariesDir = 'src/test/resources'

task cleanTorBinaries {
    doLast {
        delete fileTree(torBinariesDir) { include '*.zip' }
    }
}

clean.dependsOn cleanTorBinaries

task unpackTorBinaries {
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torBinariesDir
        }
    }
    dependsOn cleanTorBinaries
}

processResources {
    inputs.dir torBinariesDir
    dependsOn unpackTorBinaries
}
