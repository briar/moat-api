plugins {
    id 'java-library'
    id 'com.vanniktech.maven.publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    tor
}

dependencies {
    // newer versions than okhttp 3.12.x need minSdk 21 on Android
    api 'com.squareup.okhttp3:okhttp:3.12.13'
    api 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    tor 'org.briarproject:obfs4proxy-linux:0.0.14'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

test {
    useJUnitPlatform()
}

if (RELEASE_SIGNING_ENABLED != 'false') {
    signing {
        useGpgCmd()
    }
}

def torBinariesDir = 'src/test/resources'

task cleanTorBinaries {
    doLast {
        delete fileTree(torBinariesDir) { include '*.zip' }
    }
}

clean.dependsOn cleanTorBinaries

task unpackTorBinaries {
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torBinariesDir
        }
    }
    dependsOn cleanTorBinaries
}

processResources {
    inputs.dir torBinariesDir
    dependsOn unpackTorBinaries
}
