plugins {
    id 'com.android.application'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'org.briarproject.moattest'
    compileSdk 33

    defaultConfig {
        applicationId "org.briarproject.moattest"
        minSdk 16
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "okHttp"
    productFlavors {
        ok3 {
            dimension "okHttp"
            applicationIdSuffix ".ok3"
            versionNameSuffix "-ok3"
            dependencies {
                // Use obsolete version 3.12.x for Android 4 compatibility
                implementation 'com.squareup.okhttp3:okhttp:3.12.13'
            }
        }
        ok4 {
            dimension "okHttp"
            applicationIdSuffix ".ok4"
            versionNameSuffix "-ok4"
            defaultConfig {
                minSdk 21
            }
            dependencies {
                implementation 'com.squareup.okhttp3:okhttp:4.12.0'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    tor
}

dependencies {
    implementation project(':lib')
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.dagger:hilt-android:2.48'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'

    tor 'org.briarproject:lyrebird-android:0.5.0-3'

    annotationProcessor 'com.google.dagger:hilt-compiler:2.48'
}

def torLibsDir = 'src/main/jniLibs'

task cleanTorBinaries {
    outputs.dir torLibsDir
    doLast {
        delete fileTree(torLibsDir)
    }
}

clean.dependsOn cleanTorBinaries

task unpackTorBinaries {
    outputs.dir torLibsDir
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torLibsDir
        }
    }
    dependsOn cleanTorBinaries
}

preBuild.dependsOn unpackTorBinaries
